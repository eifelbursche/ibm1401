               JOB  WIPEDISK.S 04/17/23 14:51:22                           -5882
               CTL  6611
     ******************************************************************
     *  WIPES THE 1405 DISK - SIGNLE SECTORS. USE WITH CARE
     ******************************************************************
               ORG  400
     *  SOME PRE-INITIALIZATION
     START     CW   SECT1
               SW   GRPMRK
     *  PRITN START MSG
               CS   332
               CS
               MCW  STRMSG,280
               W
     *  WRITE EMPTY DATA TO A SECTOR
     NXTSEC    MCW  INCR,STEP       * RESET STEP
               SD   DSKADD-7        * MOVE ACCESS ARM
               A    INCR,STEP       * INCREMENT STEP
               WD   DSKADD-7        * WRITE SECTOR
               BIN  DSKERR,Y        * CHECK FOR DISK ERROR
               A    INCR,STEP       * INCREMENT STEP
               WDC  DSKADD-7        * WRITE DISK CHECK
               BIN  DSKERR,Y        * CHECK FOR DISK ERROR
               C    DSKADD,ENDADD   * ALL SECTORS WRITTEN?
               BE   FINI            *  THEN GOTO FINI
               A    INCR,DSKADD     * ADD 1 SECTOR TO ADDR
               BIN  ABORT,G         * ABORT IF SENSE SWITCH G IS FLIPPED
               B    NXTSEC          * WRITE NEXT SECTOR
     DSKERR    CS   332             * CLEAR PRINT AREA
               CS                   * CLEAR PRINT AREA
               MCW  STEP,280        * PRINT STEP NO
               W
               BIN  UNADCL,X        * BRANCH TO UNEQUAL-ADDR COMPARE
               BIN  WRLENR,W        * BRANCH TO WRONG-LENGTH RECORD
               BIN  RWPARC,V        * READ- OR WRITE PARITY CHECK...
     *
               CS   299
               MCW  DERRN,280       * PRINT ACCESS INOPERABLE ERROR AND HALT
               W
               B    HALT
     UNADCL    CS   299
               MCW  DERRX,280       * PRINT ERROR MSG AND HALT
               W
               B    HALT
     WRLENR    CS   299
               MCW  DERRW,280       * PRINT ERROR MSG AND HALT
               W
               B    HALT
     RWPARC    CS   299
               MCW  DERRV,280       * PRINT ERROR MSG AND HALT
               W
               B    HALT
     FINI      CS   299
               MCW  FINMSG,280      * PRINT FINAL MSG AND HALT
               W
               H    FINI
     ABORT     CS   299
               MCW  @ABORTED BY SENSE SWITCH G@,280
               W
               H    ABORT
     HALT      CS   299
               MCW  HLTMSG,280      * PRINT ERROR AND HALT
               W
               H    HALT
               H
     *
     DSKADD    DCW  00000000
     GRPMRK    DC   @g@
     SECT1     DA   1X200
     GRPWM     DCW  @g@
     STEP      DCW  000
     INCR      DCW  010
     ENDADD    DCW  00999950
     DERRN     DCW  @ACCESS INOPERABLE@
     DERRV     DCW  @READ- OR WRITE PARITY CHECK OR READ-BACK CHECK ERR@
     DERRW     DCW  @WRONG-LENGTH RECORD@
     DERRX     DCW  @UNEQUAL-ADDRESS COMPARE@
     FINMSG    DCW  @FINISHED SUCESSFULLY@
     HLTMSG    DCW  @HALTED DUE TO ERROR@
     STRMSG    DCW  @START. SENSE SWITCH G ABORTS.@
     *
               END  START
